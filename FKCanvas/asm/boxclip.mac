;
;                ..;;;;,                     ;;;,    
;            ..;;;"` ;;:           ,;;;;;: ,;;;:.,;..          _/
;      ,;..,;;"`    :;;'            :;;"`,;;;;;;"":;;         _/ 
;      ;;;"  `:;;. ;;'           ..;;:  .;;,.;;:',;;"    _/_/_/_/_/
;     .;;`   ,;;" .;"          ,;;;;;;" :;`',;",;;"         _/
;    ,;;,:.,;;;  ,;:          :" ,;:` , `:.;;;;;'`         _/   
;    ;;"'':;;:. .;; .          ,;;;,;:;;,;;;, ,;             _/
;   :;;..;;;;;; :;' :.        :;;;"` `:;;;;;,;,.;.          _/
; .;;":;;`  '"";;:  ';;       '""   .;;`.;";:;;;;` ;,  _/_/_/_/_/
;;;;" `'       "::. ,;;:          .;"`  ::. '   .,;;;     _/ 
;""             ';;;;;;;"        ""     ';;;;;;;;;;`     _/
;
;                       Windsoul++
;
;                 基于 DirectX 的游戏程序库               
;       A Game Programming Library Base On DirectX    
; 
;               by 云风 (Cloud Wu)  1999-2000                          
;
;       http://member.netease.com/~cloudwu 
;		mailto:cloudwu@263.net
; 
;		请阅读 readme.txt 中的版权信息
;		See readme.txt for copyright information.
;
;		Description:		矩形 Blit 剪裁宏
;		Original Author:	云风
;		Authors:
;		Create Time:		2001/8/24
;		Modify Time:
;
;:*W*:._.:*I*:._.:*N*:._.:*D*:._.:*S*:._.:*O*:._.:*U*:._.:*L*:._.:

%ifndef _BOXCLIP_
%define _BOXCLIP_

%imacro boxclip 1

	mov eax,[ecx+WBmpStruct.ky]
	mov edi,[ecx+WBmpStruct.ptr]	; 目标位图的数据指针
	mov edx,[ebx+WBoxStruct.h]	; 读出矩形的高度
	mov var(%$h),edx
	sub eax,[ebx+WBoxStruct.ky]	; eax = BLIT 从源位图的行位置
	jns .under_firstline

	add var(%$h),eax		; 修正需要BLIT的行数
	jle .endclip
	mov eax,var(%$h)
	jmp .adjust_height
.endclip:
	xor eax,eax
	endproc -1
	
.under_firstline:
	cmp eax,[ecx+WBmpStruct.h]	; 比较目标位图的高度
	jge .endclip
	mov edx,eax
	add eax,var(%$h)
	imul edx,[ecx+WBmpStruct.pitch]	; 修正目标位图指针
	add edi,edx

.adjust_height:
	
	sub eax,[ecx+WBmpStruct.h]	; Box 的下端和 目标图下端差(负的)
	jl .dealwith_width
	sub var(%$h),eax		; 修正需要 blit 的行数

.dealwith_width:
	mov edx,[ebx+WBoxStruct.w]	; 读出 box 的宽度
	mov eax,[ecx+WBmpStruct.kx]
	mov var(%$w),edx
	sub eax,[ebx+WBmpStruct.kx]	; eax = BLIT 到目标位图的列位置
	jns .right_firstcol

	add var(%$w),eax		; 修正需要 BLIT 的列数
	jle .endclip
	mov eax,var(%$w)
	jmp .adjust_width

.right_firstcol:
	cmp eax,[ecx+WBmpStruct.w]	; 比较目标位图的宽度
	jge .endclip			; 超出位图则跳转
%if %1 = 8
	add edi,eax
%elif %1 = 16
	lea edi,[edi+eax*2]		; 修正 this 位图指针
%elif %1 = 24
	lea edi,[edi+eax*2]
	add edi,eax
%elif %1 = 32
	lea edi,[edi+eax*4]
%endif
	add eax,var(%$w)

.adjust_width:
	sub eax,[ecx+WBmpStruct.w]	; bmp 图的右端和目标位图右端差(负的)
	jl .blit
	sub var(%$w),eax

%endmacro

%endif